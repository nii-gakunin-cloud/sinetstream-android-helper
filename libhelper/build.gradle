plugins {
  id 'com.android.library'
  id 'maven-publish'
}

android {
    compileSdk 33
    buildToolsVersion '33.0.2'

    defaultConfig {
        minSdk 19
        targetSdk 33
        versionCode currentCode as Integer
        versionName currentVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    packagingOptions {
        jniLibs {
            excludes += ['META-INF/LICENSE*']
        }
        resources {
            excludes += ['META-INF/LICENSE*']
        }
    }

    /*
     * Adapt to the domain-specific language (DSL), introduced at AGP 7.1.
     * https://stackoverflow.com/questions/71365373/software-components-will-not-be-created-automatically-for-maven-publishing-from
     * https://developer.android.com/studio/publish-library/configure-pub-variants#single-pub-var
     */
    /*
     * Hmm, this part causes the following pipeline task error.
     * ==
     * Execution failed for task ':libhelper:publishMavenPublicationToMavenRepository'.
     * > Failed to publish publication 'maven' to repository 'maven'
     *  > Invalid publication 'maven': multiple artifacts with the identical extension and classifier ('jar', 'sources').
     *
    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
     */

    namespace 'jp.ad.sinet.stream.android.helper'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'com.google.android.gms:play-services-location:21.0.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
  publishing {
    publications {
      maven(MavenPublication) {
        from components.release
        artifact androidSourcesJar
        groupId = 'jp.ad.sinet.stream.android.helper'
        artifactId = 'libhelper'
        version = currentVersion
      }
    }
    repositories {
      maven {
        url = System.getenv('MAVEN_ENDPOINT')
        credentials(HttpHeaderCredentials) {
          name = "Job-Token"
          value = System.getenv('CI_JOB_TOKEN')
        }
        authentication {
          header(HttpHeaderAuthentication)
        }
      }
    }
  }
}
